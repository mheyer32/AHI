dnl $Id$


dnl Process this file with autoconf to produce a configure script.

AC_REVISION( $Revision$ )
dnl
AC_INIT( TODO )


dnl General

AC_CANONICAL_SYSTEM
AC_PROG_MAKE_SET
AC_CONFIG_HEADER( config.h )

AC_MSG_CHECKING(CPU to build for)
AC_ARG_WITH(CPU,
[  --with-cpu              The CPU to optimize for],
CPU=${withval}, unset CPU)
AC_MSG_RESULT(${CPU})

AC_MSG_CHECKING(OS include directory)
AC_ARG_WITH(os-includedir,
[  --with-os-includedir    Path to AmigaOS include directory [/gg/os-include]],
os_includedir=${withval},
os_includedir=/gg/os-include)
AC_MSG_RESULT(${os_includedir} )

AC_MSG_CHECKING(if WarpUp support code should be enabled)
AC_ARG_ENABLE(warpup,
[  --enable-warpup         Enable WarpUp support],
AC_DEFINE(ENABLE_WARPUP))
ENABLE_WARPUP=${enable_warpup}
AC_SUBST(ENABLE_WARPUP)
AC_MSG_RESULT(${enable_warpup})

case $host in
 i?86be-pc-amithlon)
  : ${CPU="i686"}
  linkerflags="-noixemul -r"
  optflags="-mcpu=${CPU} -O2"
  asflags="--defsym CPU=${CPU}"
  resident=""   dnl *** Not supported (yet?)
  stripflag=""
 ;;
 m68k-unknown-amigaos)
  : ${CPU="68020"}
  linkerflags="-noixemul"
  optflags="-m${CPU} -O2 -fomit-frame-pointer -ffast-math"
  asflags="-m${CPU} --register-prefix-optional --bitwise-or --defsym CPU=${CPU}"
  resident="-resident"
  stripflag=""
 ;;
 powerpc-unknown-morphos)
  : ${CPU="603"}
  AC_DEFINE(USE_INLINE_STDARG)
  linkerflags="-noixemul"
  optflags="-mcpu=${CPU} -O2"
  asflags="-m${CPU} -mregnames --defsym CPU=${CPU}"
    resident="-mresident32"
  stripflag=""
 ;;
 i?86-*-aros)
 : ${CPU="i386"}
  optflags="-mcpu=${CPU} -O2 -fomit-frame-pointer"
  asflags="--defsym CPU=${CPU} --defsym LITTLE_ENDIAN=1"
  resident=""   dnl *** Not supported (yet?)
  stripflag="--strip-unneeded"
 ;;
 *)
  AC_MSG_WARN(Unknown architecture!)
 ;;
esac

top_builddir=$(pwd)

: ${CFLAGS="-g -W -Wall -Wstrict-prototypes -Wno-unused ${optflags}"}
: ${CPPFLAGS="-DCPU=\"\\\"${CPU}\\\"\" -I. -I${srcdir} -I${top_builddir} -I${top_builddir}/Include/gcc -I${os_includedir}"}
: ${LDFLAGS="${linkerflags}"}
: ${ASFLAGS="${asflags}"}
: ${ARFLAGS="-ru"}
: ${STRIPFLAGS="${stripflag}"}

: ${CFLAG_RESIDENT="${resident}"}
: ${LDFLAG_RESIDENT="${resident}"}

: ${DISTDIR="/tmp/${host_alias}-ahi"}

AC_PREFIX_DEFAULT( / )

AC_SUBST(ASFLAGS)
AC_SUBST(ARFLAGS)
AC_SUBST(STRIPFLAGS)
AC_SUBST(CFLAG_RESIDENT)
AC_SUBST(LDFLAG_RESIDENT)
AC_SUBST(DISTDIR)

dnl Checks for programs.

AC_CHECK_TOOL( CC, gcc, : ) 
AC_CHECK_TOOL( AS, as, : ) 
AC_CHECK_TOOL( AR, ar, : )
AC_CHECK_TOOL( RANLIB, ranlib, : )
AC_CHECK_TOOL( STRIP, strip, : )
AC_CHECK_TOOL( OBJCOPY, objcopy, : ) 

AC_PROG_INSTALL
#AC_PROG_CC
AC_PROG_RANLIB
AC_CHECK_PROG( RM, rm, rm )
AC_CHECK_PROG( ROBODOC, robodoc, robodoc )
AC_CHECK_PROG( FLEXCAT, flexcat, flexcat )
AC_CHECK_PROG( FD2INLINE, fd2inline, fd2inline )
AC_CHECK_PROG( FD2PRAGMA, fd2pragma, fd2pragma )
AC_CHECK_PROG( LHA, lha, lha )

if test -z "$FD2INLINE"; then
 AC_MSG_ERROR( fd2inline missing. )
fi

if test -z "$FD2PRAGMA"; then
 AC_MSG_WARN( fd2pragma missing. )
else
 if test -n "`$FD2PRAGMA --help | grep -- --mode`"; then
  AC_SUBST( FD2PRAGMA_PRE, -- )
 fi
fi

if test -z "$LHA"; then
 AC_MSG_WARN( lha missing. )
fi

if test -z "$ROBODOC"; then
 AC_MSG_WARN( robodoc missing. )
fi

dnl Checks for libraries.

AC_CHECK_LIB( amiga, NewList,,      AC_MSG_ERROR( "libamiga is missing." ) )
AC_CHECK_LIB( mui,   MUI_NewObject,,AC_MSG_WARN(  "Who cares?" ) )
AC_SEARCH_LIBS( floor, m,,	    AC_MSG_ERROR( "libm is missing." ) )


dnl Checks for header files.

AC_CHECK_HEADER( CompilerSpecific.h, , AC_MSG_ERROR( "CompilerSpecific.h is missing." )) 


dnl Checks for typedefs, structures, and compiler characteristics.

AC_C_CONST
AC_C_INLINE
AC_C_BIGENDIAN

dnl Checks for library functions.


AC_OUTPUT(Makefile
          AHI/Makefile
          AHI-Handler/Makefile
          AddAudioModes/Makefile
          Device/Makefile
          Docs/Makefile
          Drivers/Makefile
          Drivers/Common/Makefile.common
          Drivers/EMU10kx/Makefile
          Drivers/Filesave/Makefile
          Drivers/Void/Makefile
	  Examples/Makefile
	  Examples/Extras/CloneAudioModes/Makefile
	  Examples/Low-level/PlaySineEverywhere/Makefile
          Include/Makefile,
          echo timestamp > stamp-h)
